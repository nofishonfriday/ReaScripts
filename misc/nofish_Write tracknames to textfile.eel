// Version 1.01
// Description: Write tracknames to textfile
// Author: nofish
// Version: 1.01
// About:
//   http://forum.cockos.com/showthread.php?t=169099

// Changelog:
//  + 1.0 Initial release
//  + 1.01 replace "-" with "" for unnamed tracks


// nofish_Write tracknames to textfile.eel
// writes a textfile 'Tracknames.txt' to project directory (\'media subfolder' if set in Project settings, '\REAPER Media' if project is not saved)
// see http://forum.cockos.com/showthread.php?t=169099


EnumProjects(-1, #project_name); // get .rpp path + project name;
match (#project_name, "") ? #project_name = "unsaved project";
  
GetProjectPath(#filepath);
strcat(#filepath, "\\Tracknames.txt");

// version for fixed directory:
// #filepath = "C:\\Tracknames\\Tracknames.txt"; // edit this path where you want the textfile (.txt) written
// NOTE: directory must exist, otherwise it throws an error !, the textfile itself is created automatically

function msg_s(m)
(
  ShowConsoleMsg(m);
  ShowConsoleMsg("\n");
);

function msg_d(m)
(
  ShowConsoleMsg(sprintf(#, "%d", m));
  ShowConsoleMsg("\n");
);


msg_s(""); // clear console
i = 0;
#written_trackname = "";

fopen(#filepath,"w"); // to delete previously written textfile
// if above is commented out, new data is appended to the previously existing textfile
file = fopen(#filepath,"a"); // open file in append mode

// write project path + project name
strcat (#project_name, "\n\n");
fwrite(file, #project_name, 0);

loop(CountTracks(0), // loop through tracks
  (track = GetTrack(0, i)) ? (  // if track pointer != 0 -> do the code inside brackets
    GetSetMediaTrackInfo_String(track, "P_NAME", #track_name, 0); // get track name
    // msg_s(#track_name); 
    
    sprintf(#written_trackname, "%03d", i+1);
    
    strcat (#written_trackname, "|"); // append "|"
    !match (#track_name, "") ? // if track_name not ""...
    (
      strcat (#written_trackname, #track_name); // append the actual trackname
      strcat (#written_trackname, "\n"); // append newline
    ) : (
      strcat (#written_trackname, "\n"); // append newline
    );
    
    file_length = fwrite(file,#written_trackname,0); // write (append) the string to file
  ); // end of GetTrack()
  i += 1; // increment track number
); // end of loop through tracks


msg_s("Done ! Tracknames written to: ");
msg_s(#filepath);

fclose(file); // close the file

